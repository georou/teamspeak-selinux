policy_module(teamspeakd, 1.0.0)

########################################
#
# Declarations
#
#permissive teamspeakd_t;

type teamspeakd_t;
type teamspeakd_exec_t;
init_daemon_domain(teamspeakd_t, teamspeakd_exec_t)

type teamspeakd_db_t;
files_type(teamspeakd_db_t)

type teamspeakd_data_ro_t;
files_type(teamspeakd_data_ro_t)

type teamspeakd_data_rw_t;
files_type(teamspeakd_data_rw_t)

type teamspeakd_log_t;
logging_log_file(teamspeakd_log_t)

type teamspeakd_tmp_t;
files_tmp_file(teamspeakd_tmp_t)

type teamspeakd_tmpfs_t;
files_tmp_file(teamspeakd_tmpfs_t)

type teamspeakd_unit_t;
systemd_unit_file(teamspeakd_unit_t)

type teamspeakd_lib_t;
files_type(teamspeakd_lib_t)

type teamspeakd_port_t;
corenet_port(teamspeakd_port_t)

########################################
#
# teamspeakd local policy
#
allow teamspeakd_t self:netlink_route_socket create_netlink_socket_perms;
allow teamspeakd_t self:tcp_socket create_stream_socket_perms;
allow teamspeakd_t self:udp_socket create_stream_socket_perms;

allow teamspeakd_t teamspeakd_port_t:tcp_socket name_bind;
allow teamspeakd_t teamspeakd_port_t:udp_socket name_bind;

manage_files_pattern(teamspeakd_t, teamspeakd_db_t, teamspeakd_db_t)

read_files_pattern(teamspeakd_t, teamspeakd_data_ro_t, teamspeakd_data_ro_t)
filetrans_pattern(teamspeakd_t, teamspeakd_data_ro_t, teamspeakd_db_t, file, "ts3server.sqlitedb")
filetrans_pattern(teamspeakd_t, teamspeakd_data_ro_t, teamspeakd_db_t, file, "ts3server.sqlitedb-wal")
filetrans_pattern(teamspeakd_t, teamspeakd_data_ro_t, teamspeakd_db_t, file, "ts3server.sqlitedb-shm")

manage_dirs_pattern(teamspeakd_t, teamspeakd_data_rw_t, teamspeakd_data_rw_t)
manage_files_pattern(teamspeakd_t, teamspeakd_data_rw_t, teamspeakd_data_rw_t)

exec_files_pattern(teamspeakd_t, teamspeakd_lib_t, teamspeakd_lib_t)

manage_dirs_pattern(teamspeakd_t, teamspeakd_log_t, teamspeakd_log_t)
manage_files_pattern(teamspeakd_t, teamspeakd_log_t, teamspeakd_log_t)
logging_log_filetrans(teamspeakd_t, teamspeakd_log_t, { dir file })

manage_dirs_pattern(teamspeakd_t, teamspeakd_tmp_t, teamspeakd_tmp_t)
manage_files_pattern(teamspeakd_t, teamspeakd_tmp_t, teamspeakd_tmp_t)
files_tmp_filetrans(teamspeakd_t, teamspeakd_tmp_t, { dir file })

manage_dirs_pattern(teamspeakd_t, teamspeakd_tmpfs_t, teamspeakd_tmpfs_t)
manage_files_pattern(teamspeakd_t, teamspeakd_tmpfs_t, teamspeakd_tmpfs_t)
fs_tmpfs_filetrans(teamspeakd_t, teamspeakd_tmpfs_t, { dir file })

corenet_tcp_bind_generic_node(teamspeakd_t)
corenet_udp_bind_generic_node(teamspeakd_t)

fs_getattr_tmpfs(teamspeakd_t)

sysnet_read_config(teamspeakd_t)
